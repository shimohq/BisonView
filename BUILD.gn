import("//bison/build/aar.gni")

import("//build/android/resource_sizes.gni")
import("//build/config/android/config.gni")
import("//build/config/locales.gni")
import("//build/config/sanitizers/sanitizers.gni")
import("//components/gwp_asan/buildflags/buildflags.gni")
import("//components/spellcheck/spellcheck_build_features.gni")
import("//gpu/vulkan/features.gni")
import("//media/media_options.gni")
import("//mojo/public/tools/bindings/mojom.gni")
import("//third_party/icu/config.gni")
import("//tools/grit/grit_rule.gni")
import("//tools/grit/repack.gni")
import("//device/vr/buildflags/buildflags.gni")
import("//pdf/features.gni")
import("//printing/buildflags/buildflags.gni")
import("//tools/resources/generate_resource_allowlist.gni")
import("//tools/v8_context_snapshot/v8_context_snapshot.gni")
import("config.gni")

group("bison") {
  testonly = true

  deps = [
    "shell",
    "test"
  ]
}

bison_aar_manifest_xml =
    "$target_gen_dir/bison_aar_manifest/AndroidManifest.xml"

if (!is_component_build) {
  android_aar("bison_view_aar") {
    android_manifest = bison_aar_manifest_xml

    output = "$root_build_dir/aars/bison_view.aar"
    product_config_java_package = "im.shimo.bison"

    deps = [
      ":bison_aar_manifest",
      ":bison_icu_assets",
      ":bison_java",
      ":bison_v8_snapshot_assets",
      ":locale_pak_assets",
      ":pak_file_assets",
    ]

    shared_libraries = [ ":libbison_view" ]

    resource_included_patterns = [
      "*/bison/*",
      "*/ui/android/*",
      "*/content/*",
    ]

    jar_excluded_patterns = [
      "im/shimo/bison/R\$*.class",
      "im/shimo/bison/R.class",

      "gen/_bison/*",
      "gen/_third_party/*",

      "javax/*",
      "android/*",
      "androidx/*",

      "com/google/android/gms/*",
      "com/google/firebase/*",

      "META-INF/*",
      "*.stamp",
      "*.readme",
      "*.properties",
      "*.txt",
    ]
  }
}

jinja_template("bison_aar_manifest") {
  input = "java/AndroidManifest.xml.jinja2"
  output = bison_aar_manifest_xml
}


android_resources("bison_aar_resources") {
  sources = [
    "java/res/values/bison_view_attrs.xml",
  ]
  android_manifest = bison_aar_manifest_xml
  android_manifest_dep = ":bison_aar_manifest"
}

java_cpp_features("bison_java_features_srcjar") {
  visibility = [ ":*" ]
  sources = [ "common/bv_features.cc" ]
  template = "java/src/im/shimo/bison/internal/BvFeatures.java.tmpl"
}


generate_jni("bison_jni_headers") {
  sources = [
    "java/src/im/shimo/bison/ContentViewRenderView.java",
    "java/src/im/shimo/bison/internal/AndroidProtocolHandler.java",
    "java/src/im/shimo/bison/internal/BvWebResourceInterceptResponse.java",
    "java/src/im/shimo/bison/internal/BvAutofillClient.java",
    "java/src/im/shimo/bison/internal/BvBrowserContext.java",
    "java/src/im/shimo/bison/internal/BvContents.java",
    "java/src/im/shimo/bison/internal/BvContentsBackgroundThreadClient.java",
    "java/src/im/shimo/bison/internal/BvContentsClientBridge.java",
    "java/src/im/shimo/bison/internal/BvContentsIoThreadClient.java",
    "java/src/im/shimo/bison/internal/BvContentsLifecycleNotifier.java",
    "java/src/im/shimo/bison/internal/BvCookieManager.java",
    "java/src/im/shimo/bison/internal/BvDevToolsServer.java",
    "java/src/im/shimo/bison/internal/BvFormDatabase.java",
    "java/src/im/shimo/bison/internal/BvMetricsServiceClient.java",
    "java/src/im/shimo/bison/internal/BvPdfExporter.java",
    "java/src/im/shimo/bison/internal/BvPermissionRequest.java",
    "java/src/im/shimo/bison/internal/BvQuotaManagerBridge.java",
    "java/src/im/shimo/bison/internal/BvRenderProcess.java",
    "java/src/im/shimo/bison/internal/BvSettings.java",
    "java/src/im/shimo/bison/internal/BvVariationsSeedBridge.java",
    "java/src/im/shimo/bison/internal/BvWebContentsDelegate.java",
    "java/src/im/shimo/bison/internal/BvWebResourceOverrideRequest.java",
    "java/src/im/shimo/bison/internal/BvWebResourceResponse.java",
    "java/src/im/shimo/bison/internal/InputStreamUtil.java",
    "java/src/im/shimo/bison/internal/BvHttpAuthHandler.java",
    "java/src/im/shimo/bison/internal/WebMessageListenerInfo.java",
    "java/src/im/shimo/bison/internal/WebMessageListenerHolder.java",
    "java/src/im/shimo/bison/internal/JsReplyProxy.java",
    "java/src/im/shimo/bison/internal/BvDarkMode.java",
    "java/src/im/shimo/bison/internal/BvFeatureList.java",
    "java/src/im/shimo/bison/internal/BvContentsStatics.java",
    #"java/src/im/shimo/bison/internal/VariationsSeedLoader.java",
  ]
}

android_library("bison_java") {
  resources_package = "im.shimo.bison"
  sources = [

    "java/src/im/shimo/bison/BisonInitializer.java",
    "java/src/im/shimo/bison/BisonViewRenderProcessClient.java",
    "java/src/im/shimo/bison/BisonResources.java",
    "java/src/im/shimo/bison/BisonView.java",
    "java/src/im/shimo/bison/BisonViewAndroidDelegate.java",
    "java/src/im/shimo/bison/BisonViewClient.java",
    "java/src/im/shimo/bison/BisonViewDatabase.java",
    "java/src/im/shimo/bison/BisonViewPrintDocumentAdapter.java",
    "java/src/im/shimo/bison/BisonViewRenderProcess.java",
    "java/src/im/shimo/bison/BisonWebChromeClient.java",
    "java/src/im/shimo/bison/BvGeolocationCallback.java",
    "java/src/im/shimo/bison/CleanupReference.java",
    "java/src/im/shimo/bison/ClientCertRequest.java",
    "java/src/im/shimo/bison/ContentView.java",
    "java/src/im/shimo/bison/ContentViewRenderView.java",
    "java/src/im/shimo/bison/GeolocationPermissions.java",
    "java/src/im/shimo/bison/HttpAuthDatabase.java",
    "java/src/im/shimo/bison/JsPromptResult.java",
    "java/src/im/shimo/bison/JsResult.java",
    "java/src/im/shimo/bison/PermissionRequest.java",
    "java/src/im/shimo/bison/PrivilegedProcessService.java",
    "java/src/im/shimo/bison/PrivilegedProcessService0.java",
    "java/src/im/shimo/bison/PrivilegedProcessService1.java",
    "java/src/im/shimo/bison/PrivilegedProcessService2.java",
    "java/src/im/shimo/bison/SandboxedProcessService.java",
    "java/src/im/shimo/bison/ValueCallback.java",
    "java/src/im/shimo/bison/WebBackForwardList.java",
    "java/src/im/shimo/bison/WebHistoryItem.java",
    "java/src/im/shimo/bison/WebResourceRequest.java",
    "java/src/im/shimo/bison/WebResourceResponse.java",
    "java/src/im/shimo/bison/RenderProcessGoneDetail.java",
    "java/src/im/shimo/bison/WebResourceError.java",
    "java/src/im/shimo/bison/HttpAuthHandler.java",
    "java/src/im/shimo/bison/SslErrorHandler.java",
    "java/src/im/shimo/bison/ConsoleMessage.java",
    "java/src/im/shimo/bison/BisonViewSettings.java",
    "java/src/im/shimo/bison/CookieManager.java",
    "java/src/im/shimo/bison/WebMessagePort.java",
    "java/src/im/shimo/bison/WebMessage.java",
    "java/src/im/shimo/bison/BisonViewWebStorage.java",

    # adapter
    "java/src/im/shimo/bison/adapter/BisonViewRenderProcessAdapter.java",
    "java/src/im/shimo/bison/adapter/CallbackConverter.java",
    "java/src/im/shimo/bison/adapter/WebBackForwardListAdapter.java",
    "java/src/im/shimo/bison/adapter/WebHistoryItemAdapter.java",
    "java/src/im/shimo/bison/adapter/WebResourceRequestAdapter.java",
    "java/src/im/shimo/bison/adapter/GeolocationPermissionsAdapter.java",
    "java/src/im/shimo/bison/adapter/WebResourceErrorAdapter.java",
    "java/src/im/shimo/bison/adapter/BvContentsClientAdapter.java",
    "java/src/im/shimo/bison/adapter/BisonViewContentsClientAdapter.java",
    "java/src/im/shimo/bison/adapter/BisonViewRenderProcessClientAdapter.java",
    "java/src/im/shimo/bison/adapter/BisonViewSettingsAdapter.java",
    "java/src/im/shimo/bison/adapter/CookieManagerAdapter.java",
    "java/src/im/shimo/bison/adapter/BisonViewProvider.java",
    "java/src/im/shimo/bison/adapter/BisonViewWebObjectProvider.java",
    "java/src/im/shimo/bison/adapter/WebMessagePortAdapter.java",
    "java/src/im/shimo/bison/adapter/BvWebStorageAdapter.java",

    # internal
    "java/src/im/shimo/bison/internal/AndroidProtocolHandler.java",
    "java/src/im/shimo/bison/internal/BvRenderProcessGoneDetail.java",
    "java/src/im/shimo/bison/internal/BvGeolocationPermissions.java",
    "java/src/im/shimo/bison/internal/BvWebResourceInterceptResponse.java",
    "java/src/im/shimo/bison/internal/BvActionModeCallback.java",
    "java/src/im/shimo/bison/internal/BvAutofillClient.java",
    "java/src/im/shimo/bison/internal/BvBrowserContext.java",
    "java/src/im/shimo/bison/internal/BvContents.java",
    "java/src/im/shimo/bison/internal/BvContentsBackgroundThreadClient.java",
    "java/src/im/shimo/bison/internal/BvContentsClient.java",
    "java/src/im/shimo/bison/internal/BvContentsClientBridge.java",
    "java/src/im/shimo/bison/internal/BvContentsClientCallbackHelper.java",
    "java/src/im/shimo/bison/internal/BvContentsIoThreadClient.java",
    "java/src/im/shimo/bison/internal/BvContentsLifecycleNotifier.java",
    "java/src/im/shimo/bison/internal/BvCookieManager.java",
    "java/src/im/shimo/bison/internal/BvDevToolsServer.java",
    "java/src/im/shimo/bison/internal/BvFormDatabase.java",
    "java/src/im/shimo/bison/internal/BvLayoutSizer.java",
    "java/src/im/shimo/bison/internal/BvMetricsServiceClient.java",
    "java/src/im/shimo/bison/internal/BvPdfExporter.java",
    "java/src/im/shimo/bison/internal/BvPermissionRequest.java",
    "java/src/im/shimo/bison/internal/BvQuotaManagerBridge.java",
    "java/src/im/shimo/bison/internal/BvRenderProcess.java",
    "java/src/im/shimo/bison/internal/BvSafeBrowsingResponse.java",
    "java/src/im/shimo/bison/internal/BvScrollOffsetManager.java",
    "java/src/im/shimo/bison/internal/BvSettings.java",
    "java/src/im/shimo/bison/internal/BvVariationsSeedBridge.java",
    "java/src/im/shimo/bison/internal/BvViewMethods.java",
    "java/src/im/shimo/bison/internal/BvWebContentsDelegate.java",
    "java/src/im/shimo/bison/internal/BvWebContentsObserver.java",
    "java/src/im/shimo/bison/internal/BvWebResourceOverrideRequest.java",
    "java/src/im/shimo/bison/internal/BvWebResourceResponse.java",
    "java/src/im/shimo/bison/internal/ClientCertLookupTable.java",
    "java/src/im/shimo/bison/internal/InputStreamUtil.java",
    "java/src/im/shimo/bison/internal/JsDialogHelper.java",
    "java/src/im/shimo/bison/internal/JsPromptResultReceiver.java",
    "java/src/im/shimo/bison/internal/JsResultHandler.java",
    "java/src/im/shimo/bison/internal/JsResultReceiver.java",
    "java/src/im/shimo/bison/internal/OverScrollGlow.java",
    "java/src/im/shimo/bison/internal/SslUtil.java",
    "java/src/im/shimo/bison/internal/ErrorCodeConversionHelper.java",
    "java/src/im/shimo/bison/internal/BvHttpAuthHandler.java",
    "java/src/im/shimo/bison/internal/BvConsoleMessage.java",
    "java/src/im/shimo/bison/internal/BvWebResourceRequest.java",
    "java/src/im/shimo/bison/internal/ScriptReference.java",
    "java/src/im/shimo/bison/internal/WebMessageListener.java",
    "java/src/im/shimo/bison/internal/WebMessageListenerInfo.java",
    "java/src/im/shimo/bison/internal/WebMessageListenerHolder.java",
    "java/src/im/shimo/bison/internal/JsReplyProxy.java",
    "java/src/im/shimo/bison/internal/ContentHolder.java",
    "java/src/im/shimo/bison/internal/BvRunQueue.java",
    "java/src/im/shimo/bison/internal/BvContentsStatics.java",
    "java/src/im/shimo/bison/internal/FileModeConversionHelper.java",
    "java/src/im/shimo/bison/internal/WebviewErrorCode.java",
    "java/src/im/shimo/bison/internal/BvDarkMode.java",
    "java/src/im/shimo/bison/internal/DarkModeHelper.java",
    "java/src/im/shimo/bison/internal/BvFeatureList.java",

  ]

  srcjar_deps = [
    ":generate_sandboxed_service_srcjar",
    ":bison_java_features_srcjar",
    "browser:browser_enums",
  ]

  deps = [
    ":bison_aar_resources",
    ":bison_aar_manifest",
    ":bison_view_product_config_java",
    "//base:base_java",
    "//base:jni_java",
    "//build/android:build_java",
    "//components/android_autofill/browser:java",
    "//components/autofill/android:autofill_java",
    "//components/url_formatter/android:url_formatter_java",
    "//components/embedder_support/android:web_contents_delegate_java",
    "//components/embedder_support/android:util_java",
    "//components/navigation_interception/android:navigation_interception_java",
    "//components/policy/android:policy_java",
    "//components/variations/android:variations_java",
    "//components/viz/service:service_java",
    "//content/public/android:content_java",
    "//device/gamepad:java",
    "//net/android:net_java",
    "//services/network/public/mojom:mojom_java",
    "//services/network/public/mojom:url_loader_base_java",


    "//third_party/blink/public:blink_headers_java",
    "//third_party/blink/public/common:common_java",
    "//third_party/blink/public/mojom:mojom_platform_java",
    # "//services/device/public/mojom",
    # ActivityWindowAndroid requires
    "//third_party/androidx:androidx_annotation_annotation_java",
    "//third_party/androidx:androidx_appcompat_appcompat_java",
    "//third_party/androidx:androidx_core_core_java",
    "//ui/android:ui_java",
    "//url:gurl_java",
  ]

  annotation_processor_deps = [ "//base/android/jni_generator:jni_processor" ]
}

action_with_pydeps("generate_sandboxed_service_srcjar") {
  script = "//bison/build/generate_child_service.py"

  _srcjar_path = "${target_gen_dir}/${target_name}.srcjar"
  _rebased_srcjar_path = rebase_path(_srcjar_path, root_build_dir)

  args = [
    "40",
    _rebased_srcjar_path,
  ]
  outputs = [ _srcjar_path ]
}

# android_resources("bison_java_resources") {
#   sources = [ "java/res" ]
#   custom_package = "im.shimo.bison"
# }

generate_product_config_srcjar("bison_view_product_config") {
  java_package = "im.shimo.bison"
}

android_library("bison_view_product_config_java") {
  sources = [ "java/src/im/shimo/bison/BvLocaleConfig.java" ]
  srcjar_deps = [ ":bison_view_product_config" ]
  jar_excluded_patterns = [ "*/ProductConfig.class" ]
}

shared_library("libbison_view") {
  deps = [
    "//bison/lib",
    "//bison/lib:webview_entry_point",
    "//third_party/blink/public:blink",
  ]

  configs -= [ "//build/config/android:hide_all_but_jni_onload" ]
  configs += [
    "//build/config/android:hide_all_but_jni" ,
    "//build/config/compiler:chrome_orderfile_config",
  ]
}

grit("bison_resources_grit") {
  # External code should depend on ":resources" instead.
  visibility = [ ":*" ]
  source = "bison_resources.grd"
  outputs = [
    "grit/bison_resources.h",
    "bison_resources.pak",
  ]
}

copy("copy_bison_resources") {
  sources = [ "$target_gen_dir/bison_resources.pak" ]
  outputs = [ "$root_out_dir/bison_resources.pak" ]

  public_deps = [ ":bison_resources_grit" ]
}

group("resources") {
  public_deps = [ ":copy_bison_resources" ]
}

bison_repack_locales("repack_locales") {
  input_locales = platform_pak_locales
  output_locales = platform_pak_locales
}

android_assets("locale_pak_assets") {
  disable_compression = true
  renaming_sources = []
  renaming_destinations = []
  foreach(_locale, platform_pak_locales) {
    renaming_sources += [ "$root_out_dir/bison/locales/$_locale.pak" ]
    renaming_destinations += [ "stored-locales/$_locale.pak" ]
  }
  treat_as_locale_paks = true
  deps = [ ":repack_locales" ]
}

repack("repack_pack") {
  sources = [
    "$root_gen_dir/content/content_resources.pak",
    "$root_gen_dir/net/net_resources.pak",
    "$root_gen_dir/third_party/blink/public/resources/blink_resources.pak",
    "$root_gen_dir/third_party/blink/public/resources/inspector_overlay_resources.pak",
    # "$target_gen_dir/aw_resources.pak",
    # "$target_gen_dir/components_resources.pak", # generate_components_resources
    # "$target_gen_dir/mojo_bindings_resources.pak", # generate_mojo_resources
    # "$target_gen_dir/webui_resources.pak", #generate_webui_resources
  ]

  deps = [
    # ":generate_components_resources",
    # ":generate_mojo_resources",
    # ":generate_webui_resources",
    "//content:content_resources",
    "//net:net_resources",
    "//third_party/blink/public:devtools_inspector_resources",
    "//third_party/blink/public:resources",
  ]

  output = "$target_gen_dir/resources.pak"
}

repack("repack_100_percent") {
  sources = [
    "$root_gen_dir/content/app/resources/content_resources_100_percent.pak",
    "$root_gen_dir/third_party/blink/public/resources/blink_scaled_resources_100_percent.pak",
    "$root_gen_dir/ui/resources/ui_resources_100_percent.pak",
  ]
  deps = [
    "//content/app/resources",
    "//third_party/blink/public:scaled_resources_100_percent",
    "//ui/resources",
  ]
  output = "$target_gen_dir/chrome_100_percent.pak"
}

android_assets("pak_file_assets") {
  renaming_sources = [
    "$target_gen_dir/chrome_100_percent.pak",
    "$target_gen_dir/resources.pak",
  ]
  renaming_destinations = [
    "bison/chrome_100_percent.pak",
    "bison/resources.pak"
  ]
  deps = [
    ":repack_100_percent",
    ":repack_pack",
  ]
  disable_compression = true
}

android_assets("bison_icu_assets") {
  renaming_sources = [ "$root_out_dir/icudtl.dat" ]
  renaming_destinations = [ "bison/icudtl.dat" ]
  disable_compression = true
  deps = [ "//third_party/icu:icu_assets" ]
}

android_assets("bison_v8_snapshot_assets") {
  disable_compression = true
  if (current_cpu == "arm" || current_cpu == "x86" || current_cpu == "mipsel") {
    arch_suffix = "32"
  } else {
    arch_suffix = "64"
  }

  if (current_cpu == "arm" || current_cpu == "arm64") {
    abi = "arm"
  } else {
    abi = "x86"
  }
  if (use_v8_context_snapshot) {
    deps = [ "//tools/v8_context_snapshot:v8_context_snapshot_assets" ]

    renaming_sources = [ "$root_out_dir/v8_context_snapshot.bin" ]

    renaming_destinations =
        [ "bison/$abi/v8_context_snapshot_$arch_suffix.bin" ]
  } else {
    renaming_sources = [ "$root_out_dir/snapshot_blob.bin" ]

    renaming_destinations = [ "bison/$abi/snapshot_blob_$arch_suffix.bin" ]

    deps = [ "//v8:v8_external_startup_data_assets" ]
  }
}

grit("generate_components_strings") {
  source = "../components/components_strings.grd"

  defines = [
    "enable_arcore=$enable_arcore",
    "enable_pdf=$enable_pdf",
    "enable_print_preview=$enable_print_preview",
    "enable_vr=$enable_vr",
  ]
  # components_strings contains strings from all components. WebView
  # will never display most of them, so we try to limit the included
  # strings. This allowlist trims about 50% more than the compile-based
  # allowlist generated by :system_webview_pak_whitelist.
  _allowlist = rebase_path("ui/grit_strings_allowlist.txt", root_build_dir)
  inputs = [ "//bison/ui/grit_strings_allowlist.txt" ]
  grit_flags = [
    "-w",
    _allowlist,
  ]
  outputs =
      [
        "grit/components_strings.h",
        "java/res/values/components_strings.xml",
      ] +
      process_file_template(
          android_bundle_locales_as_resources,
          [ "java/res/values-{{source_name_part}}/components_strings.xml" ]) +
      process_file_template(all_chrome_locales,
                            [ "components_strings_{{source_name_part}}.pak" ])
}
