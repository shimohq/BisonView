import("//build/config/android/config.gni")
import("//build/config/features.gni")
import("//build/config/sanitizers/sanitizers.gni")
import("//build/config/ui.gni")
import("//gpu/vulkan/features.gni")
import("//media/media_options.gni")
import("//mojo/public/tools/bindings/mojom.gni")

#import("//ppapi/buildflags/buildflags.gni")
import("//tools/grit/grit_rule.gni")
import("//tools/grit/repack.gni")
import("//tools/v8_context_snapshot/v8_context_snapshot.gni")
import("config.gni")
group("bison") {
  deps = [
    "aar:bison_view_aar",
    "apk:bison_apk_java",
  ]
}

config("bison_lib_warnings") {
  if (is_clang) {
    cflags = [ "-Wno-nonnull" ]
  }
}

generate_jni("bison_jni_headers") {
  sources = [
    "java/src/im/shimo/bison/AndroidProtocolHandler.java",
    "java/src/im/shimo/bison/BisonContents.java",
    "java/src/im/shimo/bison/BisonContentsClientBridge.java",
    "java/src/im/shimo/bison/BisonWebResourceInterceptResponse.java",
    "java/src/im/shimo/bison/BisonWebResourceResponse.java",
    "java/src/im/shimo/bison/InputStreamUtil.java",
    "java/src/im/shimo/bison/BisonContentsBackgroundThreadClient.java",
    "java/src/im/shimo/bison/BisonContentsIoThreadClient.java",
    "java/src/im/shimo/bison/BisonSettings.java",
    "java/src/im/shimo/bison/BisonCookieManager.java",
    "java/src/im/shimo/bison/BisonBrowserContext.java",
    "java/src/im/shimo/bison/BisonQuotaManagerBridge.java",
    "java/src/im/shimo/bison/BisonDevToolsServer.java",
    "java/src/im/shimo/bison/BisonPermissionRequest.java",
    "java/src/im/shimo/bison/BisonVariationsSeedBridge.java",
    # "java/src/im/shimo/bison/BisonWebContentsDelegate.java",
  ]
}

android_library("bison_java") {
  
  min_sdk_version = 21

  java_files = [
    "java/src/im/shimo/bison/BisonContents.java",
    "java/src/im/shimo/bison/BisonWebChromeClient.java",
    "java/src/im/shimo/bison/BisonView.java",
    "java/src/im/shimo/bison/BisonViewAndroidDelegate.java",
    "java/src/im/shimo/bison/BisonViewClient.java",
    "java/src/im/shimo/bison/CallbackConverter.java",
    "java/src/im/shimo/bison/BisonContentsClientBridge.java",
    "java/src/im/shimo/bison/JsPromptResultReceiver.java",
    "java/src/im/shimo/bison/JsResultHandler.java",
    "java/src/im/shimo/bison/JsResultReceiver.java",
    "java/src/im/shimo/bison/BisonContentsClient.java",
    "java/src/im/shimo/bison/JsResult.java",
    "java/src/im/shimo/bison/JsPromptResult.java",
    "java/src/im/shimo/bison/WebResourceRequest.java",
    "java/src/im/shimo/bison/BisonWebContentsDelegate.java",
    "java/src/im/shimo/bison/JsDialogHelper.java",
    "java/src/im/shimo/bison/BisonWebContentsObserver.java",
    "java/src/im/shimo/bison/BisonResources.java",
    "java/src/im/shimo/bison/AndroidProtocolHandler.java",
    "java/src/im/shimo/bison/BisonWebResourceResponse.java",
    "java/src/im/shimo/bison/BisonWebResourceInterceptResponse.java",
    "java/src/im/shimo/bison/BisonContentsBackgroundThreadClient.java",
    "java/src/im/shimo/bison/BisonContentsIoThreadClient.java",
    "java/src/im/shimo/bison/BisonSettings.java",
    "java/src/im/shimo/bison/InputStreamUtil.java",
    "java/src/im/shimo/bison/CleanupReference.java",
    "java/src/im/shimo/bison/BisonContentsClientCallbackHelper.java",
    "java/src/im/shimo/bison/BisonSafeBrowsingResponse.java",
    "java/src/im/shimo/bison/BisonContentsLifecycleNotifier.java",
    "java/src/im/shimo/bison/BisonGeolocationPermissions.java",
    "java/src/im/shimo/bison/SslUtil.java",
    "java/src/im/shimo/bison/BisonQuotaManagerBridge.java",
    "java/src/im/shimo/bison/BisonDevToolsServer.java",
    "java/src/im/shimo/bison/BisonCookieManager.java",
    "java/src/im/shimo/bison/BisonBrowserContext.java",
    "java/src/im/shimo/bison/PrivilegedProcessService.java",
    "java/src/im/shimo/bison/PrivilegedProcessService0.java",
    "java/src/im/shimo/bison/PrivilegedProcessService1.java",
    "java/src/im/shimo/bison/PrivilegedProcessService2.java",
    "java/src/im/shimo/bison/SandboxedProcessService.java",
    "java/src/im/shimo/bison/BisonPermissionRequest.java",
    "java/src/im/shimo/bison/PermissionRequest.java",
    "java/src/im/shimo/bison/BisonGeolocationCallback.java",
    "java/src/im/shimo/bison/GeolocationPermissions.java",
    "java/src/im/shimo/bison/ValueCallback.java",
    "java/src/im/shimo/bison/ClientCertLookupTable.java",
    "java/src/im/shimo/bison/BisonVariationsSeedBridge.java",
    "java/src/im/shimo/bison/ClientCertRequest.java",
  ]

  srcjar_deps = [
    ":generate_sandboxed_service_srcjar",
    "browser:browser_enums",
  ]

  deps = bison_java_deps

  annotation_processor_deps = [ "//base/android/jni_generator:jni_processor" ]
}

action("generate_sandboxed_service_srcjar") {
  script = "//bison/build/generate_child_service.py"
  depfile = "$target_gen_dir/$target_name.d"

  _srcjar_path = "${target_gen_dir}/${target_name}.srcjar"
  _rebased_srcjar_path = rebase_path(_srcjar_path, root_build_dir)

  args = [
    "--depfile",
    rebase_path(depfile, root_build_dir),
    "40",
    _rebased_srcjar_path,
  ]
  outputs = [
    _srcjar_path,
  ]
}

android_resources("bison_java_resources") {
  resource_dirs = [ "java/res" ]
  custom_package = "im.shimo.bison"
}

shared_library("libbison") {
  sources = [
    "lib/bison_main_delegate.cc",
    "lib/bison_main_delegate.h",
    "lib/bison_view_library_loader.cc",
  ]

  deps = [
    ":bison_lib",
    "//base",
    "//components/crash/content/app",
    "//components/crash/content/browser",
    "//components/embedder_support/android:view",
    "//components/keyed_service/content",
    "//content/public/app:both",
    "//content/public/browser",
    "//content/public/renderer",
    "//ui/gfx",
  ]

  configs -= [ "//build/config/android:hide_all_but_jni_onload" ]
  configs += [ "//build/config/android:hide_all_but_jni" ]
}

source_set("bison_lib") {
  deps = [
    ":pak",
    "browser",
    "common",
    "gpu",
    "renderer",
    "//base",
    "//content/public/gpu",
    "//ui/gfx",
  ]
}

grit("bison_resources_grit") {
  # External code should depend on ":resources" instead.
  visibility = [ ":*" ]
  source = "bison_resources.grd"
  outputs = [
    "grit/bison_resources.h",
    "bison_resources.pak",
  ]
}

copy("copy_bison_resources") {
  sources = [
    "$target_gen_dir/bison_resources.pak",
  ]
  outputs = [
    "$root_out_dir/bison_resources.pak",
  ]

  public_deps = [
    ":bison_resources_grit",
  ]
}

group("resources") {
  public_deps = [
    ":copy_bison_resources",
  ]
}

repack("pak") {
  sources = [
    "$root_gen_dir/bison/bison_resources.pak",
    "$root_gen_dir/content/app/resources/content_resources_100_percent.pak",
    "$root_gen_dir/content/browser/tracing/tracing_resources.pak",
    "$root_gen_dir/content/content_resources.pak",
    "$root_gen_dir/mojo/public/js/mojo_bindings_resources.pak",
    "$root_gen_dir/net/net_resources.pak",
    "$root_gen_dir/third_party/blink/public/resources/blink_resources.pak",
    "$root_gen_dir/third_party/blink/public/resources/blink_scaled_resources_100_percent.pak",
    "$root_gen_dir/third_party/blink/public/strings/blink_strings_en-US.pak",
    "$root_gen_dir/ui/resources/ui_resources_100_percent.pak",
    "$root_gen_dir/ui/resources/webui_resources.pak",
    "$root_gen_dir/ui/strings/app_locale_settings_en-US.pak",
    "$root_gen_dir/ui/strings/ui_strings_en-US.pak",
  ]

  deps = [
    ":resources",
    "//content:resources",
    "//content/app/resources",
    "//content/browser/tracing:resources",
    "//mojo/public/js:resources",
    "//net:net_resources",
    "//third_party/blink/public:resources",
    "//third_party/blink/public:scaled_resources_100_percent",
    "//third_party/blink/public/strings",
    "//ui/resources",
    "//ui/strings",
  ]

  output = "$root_out_dir/bison.pak"
}
