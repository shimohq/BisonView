import("//build/config/android/config.gni")
import("//build/config/android/rules.gni")
import("//build/config/locales.gni")
import("//build/config/features.gni")
import("//build/config/sanitizers/sanitizers.gni")
import("//build/config/ui.gni")
import("//gpu/vulkan/features.gni")
import("//media/media_options.gni")
import("//mojo/public/tools/bindings/mojom.gni")
import("//tools/grit/grit_rule.gni")
import("//tools/grit/repack.gni")
import("//tools/v8_context_snapshot/v8_context_snapshot.gni")
import("//components/spellcheck/spellcheck_build_features.gni")
import("//components/gwp_asan/buildflags/buildflags.gni")
import("//third_party/icu/config.gni")

import("config.gni")
# import("//bison/build/locales.gni")
import("//bison/build/aar.gni")


group("bison") {
  deps = [
    ":bison_view_aar",
    "test",
  ]
}

bison_aar_manifest_xml =
    "$target_gen_dir/bison_aar_manifest/AndroidManifest.xml"

android_aar("bison_view_aar") {
  android_manifest = bison_aar_manifest_xml

  output = "$root_build_dir/aars/bison_view.aar"

  deps = [
    ":bison_aar_manifest",
    ":bison_java",
    ":pak_file_assets",
    ":locale_pak_assets",
    ":bison_icu_assets",
    ":bison_v8_snapshot_assets",
  ]

  shared_libraries = [ ":libbison_view" ]

  resource_included_patterns = [
    "*/bison/*",
    "*/ui/android/*",
    "*/content/*",
  ]

  jar_excluded_patterns = [
    "im/shimo/bison/R\$*.class",
    "im/shimo/bison/R.class",

    "javax/*",
    "android/*",
    "androidx/*",
    "com/google/*",
    "META-INF/*",
    "*.stamp",
    "*/.readme",
    "*/*.properties",
  ]
}

jinja_template("bison_aar_manifest") {
  input = "java/AndroidManifest.xml.jinja2"
  output = bison_aar_manifest_xml
}

generate_jni("bison_jni_headers") {
  sources = [
    "java/src/im/shimo/bison/AndroidProtocolHandler.java",
    "java/src/im/shimo/bison/BisonAutofillClient.java",
    "java/src/im/shimo/bison/BisonBrowserContext.java",
    "java/src/im/shimo/bison/BisonContents.java",
    "java/src/im/shimo/bison/BisonContentsBackgroundThreadClient.java",
    "java/src/im/shimo/bison/BisonContentsClientBridge.java",
    "java/src/im/shimo/bison/BisonContentsIoThreadClient.java",
    "java/src/im/shimo/bison/BisonContentsLifecycleNotifier.java",
    "java/src/im/shimo/bison/BisonCookieManager.java",
    "java/src/im/shimo/bison/BisonDevToolsServer.java",
    "java/src/im/shimo/bison/BisonFormDatabase.java",
    "java/src/im/shimo/bison/BisonMetricsServiceClient.java",
    "java/src/im/shimo/bison/BisonPdfExporter.java",
    "java/src/im/shimo/bison/BisonPermissionRequest.java",
    "java/src/im/shimo/bison/BisonQuotaManagerBridge.java",
    "java/src/im/shimo/bison/BisonRenderProcess.java",
    "java/src/im/shimo/bison/BisonSettings.java",
    "java/src/im/shimo/bison/BisonVariationsSeedBridge.java",
    "java/src/im/shimo/bison/BisonWebContentsDelegate.java",
    "java/src/im/shimo/bison/BisonWebResourceInterceptResponse.java",
    "java/src/im/shimo/bison/BisonWebResourceResponse.java",
    "java/src/im/shimo/bison/ContentViewRenderView.java",
    "java/src/im/shimo/bison/InputStreamUtil.java",
  ]
}

android_library("bison_java") {
  sources = [
    "java/src/im/shimo/bison/BisonContents.java",
    "java/src/im/shimo/bison/BisonWebChromeClient.java",
    "java/src/im/shimo/bison/BisonView.java",
    "java/src/im/shimo/bison/BisonViewAndroidDelegate.java",
    "java/src/im/shimo/bison/BisonViewClient.java",
    "java/src/im/shimo/bison/CallbackConverter.java",
    "java/src/im/shimo/bison/BisonContentsClientBridge.java",
    "java/src/im/shimo/bison/JsPromptResultReceiver.java",
    "java/src/im/shimo/bison/JsResultHandler.java",
    "java/src/im/shimo/bison/JsResultReceiver.java",
    "java/src/im/shimo/bison/BisonContentsClient.java",
    "java/src/im/shimo/bison/JsResult.java",
    "java/src/im/shimo/bison/JsPromptResult.java",
    "java/src/im/shimo/bison/WebResourceRequest.java",
    "java/src/im/shimo/bison/WebResourceResponse.java",
    "java/src/im/shimo/bison/BisonWebContentsDelegate.java",
    "java/src/im/shimo/bison/JsDialogHelper.java",
    "java/src/im/shimo/bison/BisonWebContentsObserver.java",
    "java/src/im/shimo/bison/BisonResources.java",
    "java/src/im/shimo/bison/AndroidProtocolHandler.java",
    "java/src/im/shimo/bison/BisonWebResourceResponse.java",
    "java/src/im/shimo/bison/BisonWebResourceInterceptResponse.java",
    "java/src/im/shimo/bison/BisonContentsBackgroundThreadClient.java",
    "java/src/im/shimo/bison/BisonContentsIoThreadClient.java",
    "java/src/im/shimo/bison/BisonSettings.java",
    "java/src/im/shimo/bison/InputStreamUtil.java",
    "java/src/im/shimo/bison/CleanupReference.java",
    "java/src/im/shimo/bison/BisonContentsClientCallbackHelper.java",
    "java/src/im/shimo/bison/BisonSafeBrowsingResponse.java",
    "java/src/im/shimo/bison/BisonContentsLifecycleNotifier.java",
    "java/src/im/shimo/bison/BisonGeolocationPermissions.java",
    "java/src/im/shimo/bison/SslUtil.java",
    "java/src/im/shimo/bison/BisonQuotaManagerBridge.java",
    "java/src/im/shimo/bison/BisonDevToolsServer.java",
    "java/src/im/shimo/bison/BisonCookieManager.java",
    "java/src/im/shimo/bison/BisonBrowserContext.java",
    "java/src/im/shimo/bison/PrivilegedProcessService.java",
    "java/src/im/shimo/bison/PrivilegedProcessService0.java",
    "java/src/im/shimo/bison/PrivilegedProcessService1.java",
    "java/src/im/shimo/bison/PrivilegedProcessService2.java",
    "java/src/im/shimo/bison/SandboxedProcessService.java",
    "java/src/im/shimo/bison/BisonPermissionRequest.java",
    "java/src/im/shimo/bison/PermissionRequest.java",
    "java/src/im/shimo/bison/BisonGeolocationCallback.java",
    "java/src/im/shimo/bison/GeolocationPermissions.java",
    "java/src/im/shimo/bison/ValueCallback.java",
    "java/src/im/shimo/bison/ClientCertLookupTable.java",
    "java/src/im/shimo/bison/BisonVariationsSeedBridge.java",
    "java/src/im/shimo/bison/ClientCertRequest.java",
    "java/src/im/shimo/bison/BisonMetricsServiceClient.java",
    "java/src/im/shimo/bison/BisonAutofillClient.java",
    "java/src/im/shimo/bison/BisonFormDatabase.java",
    "java/src/im/shimo/bison/ContentViewRenderView.java",
    "java/src/im/shimo/bison/ContentView.java",
    "java/src/im/shimo/bison/BisonLayoutSizer.java",
    "java/src/im/shimo/bison/BisonRenderProcess.java",
    "java/src/im/shimo/bison/BisonSupportLibIsomorphic.java",
    "java/src/im/shimo/bison/BisonActionModeCallback.java",
    "java/src/im/shimo/bison/AutofillActionModeCallback.java",
    "java/src/im/shimo/bison/BisonPdfExporter.java",
    "java/src/im/shimo/bison/BisonInitializer.java",
    "java/src/im/shimo/bison/BisonRenderProcessClient.java",
    "java/src/im/shimo/bison/BisonRenderProcessGoneDetail.java",
    "java/src/im/shimo/bison/SslErrorHandler.java",
    "java/src/im/shimo/bison/BisonWebStorage.java",
  ]

  srcjar_deps = [
    ":generate_sandboxed_service_srcjar",
    "browser:browser_enums",
  ]

  deps = bison_java_deps
  deps += [
    ":bison_view_resources",
    ":bison_view_product_config_java"
  ]

  annotation_processor_deps = [ "//base/android/jni_generator:jni_processor" ]
}

action("generate_sandboxed_service_srcjar") {
  script = "//bison/build/generate_child_service.py"
  depfile = "$target_gen_dir/$target_name.d"

  _srcjar_path = "${target_gen_dir}/${target_name}.srcjar"
  _rebased_srcjar_path = rebase_path(_srcjar_path, root_build_dir)

  args = [
    "--depfile",
    rebase_path(depfile, root_build_dir),
    "40",
    _rebased_srcjar_path,
  ]
  outputs = [
    _srcjar_path,
  ]
}

android_resources("bison_view_resources") {
  sources = [
    "java/res/layout/bison_js_prompt.xml",
    "java/res/values/strings.xml",
  ]
  android_manifest = bison_aar_manifest_xml
  android_manifest_dep = ":bison_aar_manifest"
}

shared_library("libbison_view") {
  sources = [
    "lib/bison_main_delegate.cc",
    "lib/bison_main_delegate.h",
    "lib/bison_view_library_loader.cc",
  ]

  deps = [
    ":bison_lib",
    "//base",
    "//components/crash/core/app",
    "//components/crash/content/browser",
    "//components/keyed_service/content",
    "//content/public/app",
    "//content/public/browser",
    "//content/public/renderer",
    "//ui/gfx",
  ]

  configs -= [ "//build/config/android:hide_all_but_jni_onload" ]
  configs += [ "//build/config/android:hide_all_but_jni" ]
}

source_set("bison_lib") {
  deps = [
    "browser",
    "common",
    "gpu",
    "renderer",
    "//base",
    "//content/public/gpu",
    "//ui/gfx",
  ]
}

grit("bison_resources_grit") {
  # External code should depend on ":resources" instead.
  visibility = [ ":*" ]
  source = "bison_resources.grd"
  outputs = [
    "grit/bison_resources.h",
    "bison_resources.pak",
  ]
}

copy("copy_bison_resources") {
  sources = [
    "$target_gen_dir/bison_resources.pak",
  ]
  outputs = [
    "$root_out_dir/bison_resources.pak",
  ]

  public_deps = [
    ":bison_resources_grit",
  ]
}

group("resources") {
  public_deps = [
    ":copy_bison_resources",
  ]
}

repack("repack_100_percent"){
  sources = [
    "$root_gen_dir/content/app/resources/content_resources_100_percent.pak",
    "$root_gen_dir/third_party/blink/public/resources/blink_scaled_resources_100_percent.pak",
    "$root_gen_dir/ui/resources/ui_resources_100_percent.pak",
  ]
  deps = [
    "//content/app/resources",
    "//third_party/blink/public:scaled_resources_100_percent",
    "//ui/resources",
  ]
  output = "$root_out_dir/chrome_100_percent.pak"

}

generate_product_config_srcjar("bison_view_product_config") {
  java_package = "im.shimo.bison"
}

android_library("bison_view_product_config_java") {
  sources = [ "java/src/im/shimo/bison/BisonLocaleConfig.java" ]
  srcjar_deps = [ ":bison_view_product_config" ]
  jar_excluded_patterns = [ "*/ProductConfig.class" ]
}

bison_repack_locales("repack_locales") {
  input_locales = locales
  output_locales = locales
}

android_assets("locale_pak_assets") {
  disable_compression = true
  renaming_sources = []
  renaming_destinations = []
  foreach(_locale, locales) {
    renaming_sources += [ "$root_out_dir/bison/locales/$_locale.pak" ]
    renaming_destinations += [ "stored-locales/$_locale.pak" ]
  }
  treat_as_locale_paks = true
  deps = [ ":repack_locales" ]
}

repack("repack_pack") {
  sources = [
    "$root_gen_dir/bison/bison_resources.pak",
    "$root_gen_dir/content/content_resources.pak",
    "$root_gen_dir/net/net_resources.pak",
    "$root_gen_dir/third_party/blink/public/resources/blink_resources.pak",
    # "$target_gen_dir/components_resources.pak",
    # "$root_gen_dir/mojo/public/js/mojo_bindings_resources.pak",
    # "$target_gen_dir/webui_resources.pak",
  ]

  deps = [
    ":resources",
    "//content:content_resources",
    "//net:net_resources",
    "//third_party/blink/public:resources",
  ]

  output = "$root_out_dir/bison.pak"
}


android_assets("pak_file_assets") {
  sources = [
    "$root_out_dir/chrome_100_percent.pak",
    "$root_out_dir/bison.pak",
  ]
  disable_compression = true
  deps = [
    "//bison:repack_100_percent",
    "//bison:repack_pack",
  ]
}

android_assets("bison_icu_assets") {
  renaming_sources = ["$root_out_dir/icudtl.dat"]
  renaming_destinations = [ "bison/icudtl.dat" ]
  disable_compression = true
  deps = [
    "//third_party/icu:icu_assets",
  ]
}

android_assets("bison_v8_snapshot_assets") {

  disable_compression = true
  if (current_cpu == "arm" || current_cpu == "x86" ||
        current_cpu == "mipsel") {
    arch_suffix = "32"
  } else {
    arch_suffix = "64"
  }

  if (current_cpu == "arm" || current_cpu == "arm64") {
    abi = "arm"
  } else {
    abi = "x86"
  }
  if (use_v8_context_snapshot) {
    deps = [ "//tools/v8_context_snapshot:v8_context_snapshot_assets" ]

    renaming_sources = [
      "$root_out_dir/v8_context_snapshot.bin"
    ]

    renaming_destinations = [
      "bison/$abi/v8_context_snapshot_$arch_suffix.bin"
    ]
  } else {

    renaming_sources = [
      "$root_out_dir/snapshot_blob.bin"
    ]

    renaming_destinations = [
      "bison/$abi/snapshot_blob_$arch_suffix.bin"
    ]

    deps = [ "//v8:v8_external_startup_data_assets" ]
  }
}


grit("generate_components_strings") {
  source = "../components/components_strings.grd"

  # components_strings contains strings from all components. WebView
  # will never display most of them, so we try to limit the included
  # strings. This allowlist trims about 50% more than the compile-based
  # allowlist generated by :system_webview_pak_whitelist.
  _allowlist = rebase_path("ui/grit_strings_allowlist.txt", root_build_dir)
  inputs = [ "//bison/ui/grit_strings_allowlist.txt" ]
  grit_flags = [
    "-w",
    _allowlist,
  ]
  outputs =
      [
        "grit/components_strings.h",
        "java/res/values/components_strings.xml",
      ] +
      process_file_template(
          android_bundle_locales_as_resources,
          [ "java/res/values-{{source_name_part}}/components_strings.xml" ]) +
      process_file_template(locales_with_fake_bidi,
                            [ "components_strings_{{source_name_part}}.pak" ])
}
